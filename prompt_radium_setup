#
# A ZSH theme based on skwp prezto theme, agnoster, the robl ohmyzsh theme, and the paradox prezto theme.
#  * Single line prompt
#  * User@hostname on the left + on/off toggle with an env var
#  * Git branch info on the left
#  * Time since last commit on the left
#  * Time of execution on the right
#
# Motivation: All of the themes that I have based Radium on are great.
# As a matter of fact, I have used great code from paradox and swkp.
# However, I wanted a theme that has a dynamic propmpt with a look that
# I have control over. It was also my attempt in becoming familiar
# with ZSH. Feel free to use Radium or change it.
# This is the beauty of it all.
#
# Authors:
#   Dimitar Dimitrov <dimitrov.inc@gmail.com>
#
# Radium User Environment Variables:
#   RADIUM_USER_BLOCK=0 Disables the user block
#   RADIUM_USER_BLOCK=1 Enables the user block

RADIUM_CURRENT_BG='NONE'
RADIUM_SEPARATOR=$'\ue0b0'

# check for a git repo
function is_git_repo() {
  if `git rev-parse --git-dir > /dev/null 2>&1`; then
    return 0
  else
    return 1
  fi
}

# returns the time since last git commit
function git_time_details() {
  # only proceed if there is actually a commit
  if [[ $(git log 2>&1 > /dev/null | grep -c "^fatal: bad default revision") == 0 ]]; then
    lc_time=`git log --pretty=format:'%at' -1 2> /dev/null`

    now=`date +%s`
    seconds_since_last_commit=$((now-lc_time))
    lc_time_since=`time_since_commit $seconds_since_last_commit`

    echo "$lc_time_since"
  else
    echo ""
  fi
}

# returns the time by given seconds
function time_since_commit() {
  seconds_since_last_commit=$(($1 + 0))

  # totals
  MINUTES=$((seconds_since_last_commit / 60))
  HOURS=$((seconds_since_last_commit/3600))

  # sub-hours and sub-minutes
  DAYS=$((seconds_since_last_commit / 86400))
  SUB_HOURS=$((HOURS % 24))
  SUB_MINUTES=$((MINUTES % 60))

  if [ "$HOURS" -gt 24 ]; then
    echo "${DAYS}d${SUB_HOURS}h${SUB_MINUTES}m"
  elif [ "$MINUTES" -gt 60 ]; then
    echo "${HOURS}h${SUB_MINUTES}m"
  else
    echo "${MINUTES}m"
  fi
}

# this is the start_segment function from paradox
function prompt_radium_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$RADIUM_CURRENT_BG" != 'NONE' && "$1" != "$RADIUM_CURRENT_BG" ]]; then
    print -n " $bg%F{$RADIUM_CURRENT_BG}$RADIUM_SEPARATOR$fg "
  else
    print -n "$bg$fg "
  fi
  RADIUM_CURRENT_BG="$1"
  [[ -n "$3" ]] && print -n "$3"
}

# this is the end_segment function from paradox
function prompt_radium_end_segment {
  if [[ -n "$RADIUM_CURRENT_BG" ]]; then
    print -n " %k%F{$RADIUM_CURRENT_BG}$RADIUM_SEPARATOR"
  else
    print -n "%k"
  fi
  print -n "%f"

  RADIUM_CURRENT_BG=''
}

# TO DO: create individual functions for each block of the prompt
function prompt_radium_construct_prompt() {
  # show the user block unless it is disabled
  if [[ -n "$RADIUM_USER_BLOCK" ]]; then
    if [[ "$RADIUM_USER_BLOCK" == 1 ]]; then
      prompt_radium_start_segment black default '%F{white}%n%f%F{red}@%f%F{blue}%m%f'
    fi
  else
    prompt_radium_start_segment black default '%F{white}%n%f%F{red}@%f%F{blue}%m%f'
  fi

  # directory block of the prompt
  prompt_radium_start_segment blue black '%F{white} %~ %f'

  # show the master branch
  if [[ -n ${vcs_info_msg_0_} ]]; then
    prompt_radium_start_segment green white "${vcs_info_msg_0_}"
  fi

  # show the git time
  if is_git_repo; then
    prompt_radium_start_segment 136  white "$(git_time_details)"
  fi

  prompt_radium_end_segment
}

function prompt_radium_precmd {
  # Check for untracked files or updated submodules since vcs_info doesn't.
  if [[ ! -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    fmt_branch="%b%u%c${__PROMPT_RADIUM_COLORS[4]}●%f"
  else
    fmt_branch="%b%u%c"
  fi
  zstyle ':vcs_info:*:prompt:*' formats "${fmt_branch}"

  vcs_info 'prompt'
}

function prompt_radium_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_radium_precmd

  # use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_RADIUM_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_RADIUM_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # enable VCS systems you use.
  zstyle ':vcs_info:*' enable bzr git hg svn

  # check-for-changes disabled for performance reasons
  zstyle ':vcs_info:*:prompt:*' check-for-changes false

  # local fmt_branch="(${__PROMPT_RADIUM_COLORS[1]}%b%f%u%c)"
  local fmt_branch="${__PROMPT_RADIUM_COLORS[2]}%b%f%u%c"
  local fmt_action="${__PROMPT_RADIUM_COLORS[5]}%a%f"
  local fmt_unstaged="${__PROMPT_RADIUM_COLORS[2]}●%f"
  local fmt_staged="${__PROMPT_RADIUM_COLORS[5]}●%f"

  zstyle ':vcs_info:*:prompt:*' unstagedstr   "${fmt_unstaged}"
  zstyle ':vcs_info:*:prompt:*' stagedstr     "${fmt_staged}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch}${fmt_action}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  PROMPT='${(e)$(prompt_radium_construct_prompt)}'
  RPROMPT='%F{blue}[%F{green}%D{%H:%M:%S}%F{blue}]%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_radium_setup "$@"
